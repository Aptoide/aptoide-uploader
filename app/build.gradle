apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
  lintOptions {
    checkReleaseBuilds false
    abortOnError false
  }

  compileSdkVersion 28

  signingConfigs {
    release {
      storeFile = file(project.STORE_FILE_BACKUP_UPLOADER)
      storePassword = project.STORE_PASSWORD_BACKUP_UPLOADER
      keyAlias = project.KEY_ALIAS_UPLOADER
      keyPassword = project.KEY_PASSWORD_UPLOADER
    }
  }

  defaultConfig {
    applicationId "pt.caixamagica.aptoide.uploader"
    minSdkVersion 16
    targetSdkVersion 27
    versionCode project.VERSION_CODE_UPLOADER.toInteger()
    versionName "2.106"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    buildConfigField "String", "FLURRY_KEY_UPLOADER", "\"" + project.FLURRY_KEY_UPLOADER + "\""
    resValue "string", "facebook_app_id", "\"" + project.FACEBOOK_APP_ID_UPLOADER + "\""
    resValue "string", "fb_login_protocol_scheme",
        "\"" + "fb" + project.FACEBOOK_APP_ID_UPLOADER + "\""
    buildConfigField "String", "APTOIDE_WEB_SERVICES_RAKAM_HOST", "\"" + project.APTOIDE_WEB_SERVICES_RAKAM_HOST + "\""
    resValue "string", "google_id", "\"" + project.GMS_SERVER_ID_VANILLA + "\""
    buildConfigField "String", "RAKAM_API_KEY", "\"" + project.RAKAM_API_KEY_TEST + "\""
    vectorDrawables.useSupportLibrary = true
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      multiDexEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release

      buildConfigField "String", "SCHEMA", "\"" + project.SCHEMA_PROD + "\""
    }

    debug {
      applicationIdSuffix ".debug"
      multiDexEnabled true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "SCHEMA", "\"" + project.SCHEMA_DEV + "\""
    }
  }

  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      def project = "uploader"
      def SEP = "_"
      def buildType = variant.variantData.variantConfiguration.buildType.name
      def versionName = variant.versionName
      def versionCode = variant.versionCode

      def newApkName = project + SEP + buildType + SEP + versionName + SEP + versionCode + ".apk"

      output.outputFileName = newApkName
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dexOptions {
    javaMaxHeapSize "4g"
    dexInProcess = false
    preDexLibraries = false
  }
}

dependencies {

  // Support Libraries
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'com.google.android.material:material:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

  // Reactive Programming
  implementation "io.reactivex.rxjava2:rxjava:2.2.10"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:2.0.0"
  implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
  implementation "androidx.room:room-rxjava2:2.2.3"

  // Database
  // Room components

  kapt "androidx.room:room-compiler:2.2.3"
  implementation "androidx.room:room-runtime:2.2.3"
  androidTestImplementation "androidx.room:room-testing:2.2.3"

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // Unit Test
  testImplementation 'org.jetbrains.spek:spek-api:1.1.5'
  testImplementation 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
  testImplementation 'org.junit.platform:junit-platform-runner:1.0.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.1'
  testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
  testImplementation 'org.mockito:mockito-core:2.28.2'
  testImplementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

  // Networking
  implementation 'com.squareup.retrofit2:retrofit:2.5.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
  implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'

  // Image Loading
  implementation 'com.github.bumptech.glide:glide:4.9.0'
  kapt 'com.github.bumptech.glide:compiler:4.9.0'


  //Flurry
  implementation 'com.flurry.android:analytics:12.0.1'
  //Facebook
  implementation 'com.facebook.android:facebook-login:[5,6)'

  //Google Sign-In
  implementation 'com.google.android.gms:play-services-auth:16.0.0'

  //Play Services for Flurry
  implementation 'com.google.android.gms:play-services-base:15.0.1'

  //Chrome Custom tabs
  implementation 'com.android.support:customtabs:28.0.0'

  //Rakam
  implementation "io.rakam:android-sdk:${RAKAM_VERSION}"
}
