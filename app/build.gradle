apply plugin: 'com.android.application'

buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.+'
  }
}

def fabricApiKey = project.FABRIC_API_KEY
def fabricConfigured = fabricApiKey != null
if (fabricConfigured) {
  task fabricGenerateProperties {
    doFirst {
      def fabricProperties = project.file("fabric.properties")
      fabricProperties.delete()
      fabricProperties.append("# Do not modify this file -- YOUR CHANGES WILL BE ERASED!\n")
      fabricProperties.append("apiKey=" + fabricApiKey)
    }
  }
  preBuild.dependsOn(fabricGenerateProperties)
  apply plugin: 'io.fabric'
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
}


android {
  lintOptions {
    disable 'MissingTranslation'
    abortOnError false
  }
  signingConfigs {
    release {
      storeFile = file(project.STORE_FILE_BACKUP_UPLOADER)
      storePassword = project.STORE_PASSWORD_BACKUP_UPLOADER
      keyAlias = project.KEY_ALIAS_UPLOADER
      keyPassword = project.KEY_PASSWORD_UPLOADER
      //            v2SigningEnabled = false
    }
  }

  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "pt.caixamagica.aptoide.uploader"
    minSdkVersion 9
    targetSdkVersion 23
    Integer value = project.VERSION_CODE_UPLOADER.toInteger();
    versionCode value
    versionName "2.8"

    vectorDrawables.useSupportLibrary = true

    manifestPlaceholders = [fabricApiKey: project.FABRIC_API_KEY]

    buildConfigField "String", "GMS_SERVER_ID", "\"" + project.GMS_SERVER_ID + "\""

    resValue "string", "facebook_app_id", "\"" + project.FACEBOOK_APP_ID_UPLOADER + "\""
  }

  buildTypes {
    release {
      multiDexEnabled false
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release

      applicationVariants.all { variant ->
        variant.outputs.each { output ->
          project.ext { appName = 'aptoide-uploader' }
          def newName = output.outputFile.name
          newName = newName.replace("app-", "$project.ext.appName-")
          //"MyAppName" -> I set my app variables in the root project
          newName = newName.replace("-release", "-" + versionName)
          //noinspection GroovyAssignabilityCheck
          output.outputFile = new File(output.outputFile.parent, newName)
        }
      }
    }

    debug {
      signingConfig signingConfigs.release
      applicationIdSuffix ".debug"
      multiDexEnabled true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }


  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      def project = "uploader"
      def SEP = "_"
      def buildType = variant.variantData.variantConfiguration.buildType.name
      def versionName = variant.versionName
      def versionCode = variant.versionCode

      def newApkName = project + SEP + buildType + SEP + versionName + SEP + versionCode + ".apk"

      output.outputFile = new File(output.outputFile.parent, newApkName)
    }
  }

  packagingOptions {
    exclude 'META-INF/notice.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/license.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/ASL2.0'
  }
  useLibrary 'org.apache.http.legacy'
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  compile 'com.android.support:appcompat-v7:23.2.1'

  compile 'com.octo.android.robospice:robospice:1.4.14'
  compile 'com.octo.android.robospice:robospice-retrofit:1.4.14'

  compile 'com.fasterxml.jackson.core:jackson-databind:2.5.4'

  compile 'com.squareup.retrofit:converter-jackson:1.3.0'
  compile 'com.squareup.picasso:picasso:2.4.0'

  compile 'com.android.support:recyclerview-v7:23.2.1'

  compile 'com.github.manuelpeinado.multichoiceadapter:multichoiceadapter:3.1.0'

  compile 'com.facebook.android:facebook-android-sdk:3.23.1'
  compile 'com.google.android.gms:play-services-basement:9.8.0'
  compile 'com.google.android.gms:play-services-auth:9.8.0'
  compile 'com.google.android.gms:play-services-plus:9.8.0'

  provided 'org.projectlombok:lombok:1.16.0'

  compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
    transitive = true;
  }

  compile 'com.android.support:multidex:1.0.1'
}
